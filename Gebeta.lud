(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))
(define "OwnedHoles" 
    (if (is Mover P1)
        (sites (values Remembered "OwnedP1"))
        (sites (values Remembered "OwnedP2"))
    )
)

(define "RightMostEmpty" 
    (trackSite FirstSite "TrackCW"
        from:(mapEntry "RightMost" Mover)
        if:(is Empty (to))
    )
)

(define "RemmemberOwnedHoles" 
    (if (is Mover P1)
        (remember Value "OwnedP1" #1)
        (remember Value "OwnedP2" #1)
    )
)

(define "APlayerCanNotMove"
    (and (not (is Next Mover))
        (or
            (all Sites (sites (values Remembered "OwnedP1")) if:(is Empty (site)))
            (all Sites (sites (values Remembered "OwnedP2")) if:(is Empty (site)))
        )
    )
)

//------------------------------------------------------------------------------

(game "Gebeta"
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            {
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "5,W,N,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))         // P2 home
        (map "RightMost" {(pair P1 5) (pair P2 6)})
    }) 
    (rules 
        (start { 
            (set Count 4 to:(sites Track)) 
            (set RememberValue "OwnedP1" (sites Bottom))
            (set RememberValue "OwnedP2" (sites Top))
        })
        
        phases:{
        (phase "Sowing"
            (play
                (or {
                    (move Select 
                        (from
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                ("OwnedHoles") 
                            )
                            if:(is Occupied (from))
                        )
                        (then
                            (do
                                (set Var "NumSowed" (count at:(last To)))
                                next:(sow
                                    "TrackCCW"
                                    apply:(if (= 4 (count at:(to)))
                                        (fromTo
                                            (from (to))
                                            (to (handSite Mover))
                                            count:4
                                        )
                                        (if (< 1 (count at:(to)))
                                            (and
                                                (moveAgain)
                                                (set Var "Replay" (to))
                                            )
                                        )
                                    )
                                )
                                (then
                                    (and
                                        (forEach Site (sites Track from:(last From) to:(trackSite Move from:(last From) "Track" steps:(- (var "NumSowed") 1)))
                                            (if (= 4 (count at:(site)))
                                                (fromTo
                                                    (from (site))
                                                    (to (if (is In (to) (sites (values Remembered "OwnedP1"))) (handSite P1) (handSite P2)))
                                                    count:4
                                                )
                                            )
                                        )
                                        (set Var "NumSowed" 0)
                                    )
                                )
                            )
                        )
                    )
                    }
                    (then
                        (if ("APlayerCanNotMove")
                            (and { 
                                (if (no Moves P1)
                                    (forEach Site (sites Board)
                                        (if (is Occupied (site))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P1))
                                                count:(count at:(site))
                                            )
                                        )
                                    )
                                    (forEach Site (sites Board)
                                        (if (is Occupied (site))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P2))
                                                count:(count at:(site))
                                            )
                                        )
                                    )
                                )
                            })
                        )
                    )
                )
            )
            
            (end (if ("NoPieceOnBoard")
                    {
                    (if (> (count Cell at:(handSite P2)) (count Cell at:(handSite P1))) (result P2 Win))
                    (if (> (count Cell at:(handSite P1)) (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
        )
        
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Gebeta is a two-row mancala-style board game played in Ethiopia. It shares characteristics of similar games from the Horn of Africa, such as the capture of any holes containing four counters.")
        (aliases {"Gabata"})
        (rules "2x6 board. Four counters per hole. Sowing occurs in an anti-clockwise direction. When the final counter of a sowing lands in an occupied hole, these counters are picked up and the sowing continues. During sowing, the contents of any hole made to contain four counters are captured by the player in whose row the hole is located, except when this is the final counter of a sowing, in which case the player who is sowing captures these counters. When the sowing ends in an empty hole, the turn ends. Play continues until one player cannot move, and the other player captures the remaining counters.")
        (source "Thiel 2025.")
        
        (version "1.3.14")
        (classification "board/sow/two rows")
        (credit "Eric Piette and Oliver Thiel")
        (origin "This game was played in Norway in 2025.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Rio Kadalis_ai"
    )
)

